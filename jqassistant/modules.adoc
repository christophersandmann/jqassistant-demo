[[modules:Default]]
[role=group,includesConstraints="modules:UndefinedModules,modules:ShoppingCartDependencies",includesConcepts="modules:Dependencies.graphml"]
=== Module Structure

The application is structured into modules, the following constraints apply:

- <<modules:UndefinedModules>>
- <<modules:ShoppingCartDependencies>>

The following GraphML reports will be created:

- <<modules:Dependencies.graphml>>


==== Concepts

[[modules:Root]]
[source,cypher,role=concept]
.The root package of of the shop application is "org.jqassistant.demo"
----
MATCH
  (artifact:Artifact)-[:CONTAINS]->(root:Package)
WHERE
  artifact.type <> "test-jar"
  and root.fqn = "org.jqassistant.demo"
SET
  root:Root
RETURN
  root.fqn as RootPackage
----

[[modules:DefinedModules]]
[source,cypher,role=concept,requiresConcepts="modules:Root"]
.Modules are located in sub-packages of the root package.
----
MATCH
  (root:Root:Package)-[:CONTAINS]->(module:Package)
WHERE
  module.name in [
    "usermanagement",
    "shoppingcart",
    "dataimport"
  ]
SET
  module:Module
RETURN
  module.name as Module, module.fqn as Package
----


==== Constraints

[[modules:UndefinedModules]]
[source,cypher,role=constraint,requiresConcepts="modules:DefinedModules"]
.All modules within the root package must be explicitly defined.
----
MATCH
  (root:Root:Package)-[:CONTAINS]->(undefinedModule:Package)
WHERE NOT
  undefinedModule:Module
RETURN
  undefinedModule.fqn as UndefinedModule
----


[[modules:ShoppingCartDependencies]]
[source,cypher,role=constraint,requiresConcepts="modules:DefinedModules"]
.The module "shoppingcart" must only have dependencies to "usermanagement".
----
MATCH
  (shoppingCart:Module)-[:CONTAINS*0..]->(type1:Type),
  (otherModule:Module)-[:CONTAINS*0..]->(type2:Type),
  (type1)-[:DEPENDS_ON]->(type2)
WHERE
  shoppingCart <> otherModule
  and not otherModule.name in [
    "usermanagement"
  ]
RETURN
  otherModule.name as InvalidModule, type1.fqn as Dependent, type2.fqn as InvalidDependency
----


==== Reports

[[modules:Dependencies.graphml]]
[source,cypher,role=concept,requiresConcepts="modules:DefinedModules"]
.Creates a GraphML report about the modules and their dependencies.
----
MATCH
  (module:Module)-[:CONTAINS*0..]->(type:Type),
OPTIONAL MATCH
  (type)-[depends:DEPENDS_ON]->(:Type)
WITH
  module, collect(type) as types, collect(depends) as dependencies
RETURN
 {
    role : "graph",
    parent : module,
    nodes : types,
    relationships : dependencies
 }
----
